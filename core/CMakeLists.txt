
#-----------------------------------------------------------------------------
INCLUDE_DIRECTORIES(${CMAKE_SOURCE_DIR})
INCLUDE_DIRECTORIES(${CMAKE_CURRENT_SOURCE_DIR})
INCLUDE_DIRECTORIES(${CMAKE_CURRENT_SOURCE_DIR}/impl)
INCLUDE_DIRECTORIES(${CMAKE_BINARY_DIR})
INCLUDE_DIRECTORIES(${CMAKE_CURRENT_BINARY_DIR})
#-----------------------------------------------------------------------------

SET(TACHO_HEADERS_PUBLIC "")
SET(TACHO_HEADERS_PRIVATE "")
SET(TACHO_SOURCES "")

FILE(GLOB TACHO_HEADERS_PRIVATE impl/*.hpp)
FILE(GLOB TACHO_HEADERS_PUBLIC *.hpp)
LIST(APPEND TACHO_HEADERS_PUBLIC ${CMAKE_BINARY_DIR}/Tacho_Config.hpp )

LIST(APPEND TACHO_SOURCES
  impl/Tacho_Util.cpp
  impl/Tacho_Blas_External.cpp
  impl/Tacho_Lapack_External.cpp
  impl/Tacho_GraphTools_Metis.cpp
  impl/Tacho_SymbolicTools.cpp
)

SET(TACHO_ETI_FILE "Tacho_Driver")
SET(TACHO_ETI_DEVICE_NAME "")
SET(TACHO_ETI_DEVICE_TYPE "")
SET(TACHO_ETI_WITH_TASK "")

IF (Kokkos_ENABLE_SERIAL)
  LIST(APPEND TACHO_ETI_DEVICE_NAME "Serial")
  LIST(APPEND TACHO_ETI_DEVICE_TYPE "Kokkos::Device<Kokkos::Serial,Kokkos::HostSpace>")
  LIST(APPEND TACHO_ETI_WITH_TASK "0")
ENDIF()
IF (Kokkos_ENABLE_OPENMP)
  LIST(APPEND TACHO_ETI_DEVICE_NAME "OpenMP")
  LIST(APPEND TACHO_ETI_DEVICE_TYPE "Kokkos::Device<Kokkos::OpenMP,Kokkos::HostSpace>")
  LIST(APPEND TACHO_ETI_WITH_TASK "1")
ENDIF()
IF (Kokkos_ENABLE_CUDA)
  LIST(APPEND TACHO_ETI_DEVICE_NAME "CUDA")
  LIST(APPEND TACHO_ETI_DEVICE_TYPE "Kokkos::Device<Kokkos::Cuda,Kokkos::CudaSpace>")
  LIST(APPEND TACHO_ETI_WITH_TASK "0")
ENDIF()
IF (Kokkos_ENABLE_HIP)
  LIST(APPEND TACHO_ETI_DEVICE_NAME "HIP")
  LIST(APPEND TACHO_ETI_DEVICE_TYPE "Kokkos::Device<Kokkos::Experimental::HIP,Kokkos::Experimental::HIPSpace>")
  LIST(APPEND TACHO_ETI_WITH_TASK "0")
ENDIF()

LIST(LENGTH TACHO_ETI_DEVICE_NAME ETI_DEVICE_COUNT)
MATH(EXPR ETI_DEVICE_COUNT "${ETI_DEVICE_COUNT}-1")

SET(TACHO_ETI_VALUE_NAME "")
SET(TACHO_ETI_VALUE_TYPE "")

LIST(APPEND TACHO_ETI_VALUE_NAME "double")
LIST(APPEND TACHO_ETI_VALUE_TYPE "double")

LIST(APPEND TACHO_ETI_VALUE_NAME "float")
LIST(APPEND TACHO_ETI_VALUE_TYPE "float")

LIST(APPEND TACHO_ETI_VALUE_NAME "complex_double")
LIST(APPEND TACHO_ETI_VALUE_TYPE "Kokkos::complex<double>")

LIST(APPEND TACHO_ETI_VALUE_NAME "complex_float")
LIST(APPEND TACHO_ETI_VALUE_TYPE "Kokkos::complex<float>")

LIST(LENGTH TACHO_ETI_VALUE_NAME ETI_VALUE_COUNT)
MATH(EXPR ETI_VALUE_COUNT "${ETI_VALUE_COUNT}-1")

FOREACH(I RANGE ${ETI_DEVICE_COUNT})
  LIST(GET TACHO_ETI_DEVICE_NAME ${I} ETI_DEVICE_NAME)
  LIST(GET TACHO_ETI_DEVICE_TYPE ${I} ETI_DEVICE_TYPE)
  LIST(GET TACHO_ETI_WITH_TASK   ${I} ETI_WITH_TASK)

  FOREACH(J RANGE ${ETI_VALUE_COUNT})
    LIST(GET TACHO_ETI_VALUE_NAME ${J} ETI_VALUE_NAME)
    LIST(GET TACHO_ETI_VALUE_TYPE ${J} ETI_VALUE_TYPE)

    FOREACH(ETI_FILE IN LISTS TACHO_ETI_FILE)
      SET(ETI_NAME "${ETI_FILE}_ETI_${ETI_VALUE_NAME}_${ETI_DEVICE_NAME}")
      MESSAGE(STATUS "Generating ETI: ${ETI_NAME}.cpp")
      CONFIGURE_FILE(eti/${ETI_FILE}_ETI.in eti/${ETI_NAME}.cpp)

    LIST(APPEND TACHO_SOURCES
      eti/${ETI_NAME}.cpp)

    ENDFOREACH()
  ENDFOREACH()
ENDFOREACH()

#-----------------------------------------------------------------------------
ADD_LIBRARY(tacho STATIC ${TACHO_SOURCES})
TARGET_COMPILE_FEATURES(tacho PUBLIC cxx_std_14)
SET_TARGET_PROPERTIES(tacho PROPERTIES PUBLIC_HEADER "${TACHO_HEADERS_PUBLIC}")
IF (TACHO_ENABLE_SHARED_BUILD)
  SET_PROPERTY(TARGET tacho PROPERTY POSITION_INDEPENDENT_CODE ON)
ENDIF()

TARGET_LINK_LIBRARIES(tacho
  PUBLIC
  ${TACHO_INTERNAL_METIS_TARGET}
  ${TACHO_INTERNAL_KOKKOS_TARGET}
  ${TACHO_INTERNAL_CUDA_TARGET}
  ${TACHO_INTERNAL_OPENBLAS_TARGET}
  ${TACHO_INTERNAL_BLAS_TARGET}
  ${TACHO_INTERNAL_LAPACK_TARGET}
)

TARGET_INCLUDE_DIRECTORIES(tacho
  PUBLIC
  $<BUILD_INTERFACE:${CMAKE_CURRENT_SOURCE_DIR}>
  $<BUILD_INTERFACE:${CMAKE_CURRENT_SOURCE_DIR}/impl>
  $<INSTALL_INTERFACE:${TACHO_INSTALL_INCLUDE_PATH}>
)

INSTALL(TARGETS tacho
  EXPORT tacho-targets
  LIBRARY DESTINATION "${TACHO_INSTALL_LIB_PATH}"
  ARCHIVE DESTINATION "${TACHO_INSTALL_LIB_PATH}"
  RUNTIME DESTINATION "${TACHO_INSTALL_BIN_PATH}"
  INCLUDES DESTINATION "${TACHO_INSTALL_INCLUDE_PATH}"
  PUBLIC_HEADER DESTINATION "${TACHO_INSTALL_INCLUDE_PATH}"
)

INSTALL(EXPORT tacho-targets
  DESTINATION "${TACHO_INSTALL_LIB_PATH}/cmake/Tacho"
  NAMESPACE Tacho::
)

CONFIGURE_PACKAGE_CONFIG_FILE(
  "${CMAKE_SOURCE_DIR}/cmake/Tacho_Config.cmake.in" "${CMAKE_CURRENT_BINARY_DIR}/Tacho_Config.cmake"
  INSTALL_DESTINATION "${TACHO_INSTALL_LIB_PATH}/cmake/Tacho"
)

WRITE_BASIC_PACKAGE_VERSION_FILE(
  "${PROJECT_BINARY_DIR}/tacho-config-version.cmake"
  COMPATIBILITY SameMajorVersion 
)

INSTALL(FILES "${CMAKE_CURRENT_BINARY_DIR}/Tacho_Config.cmake"
  DESTINATION "${TACHO_INSTALL_LIB_PATH}/cmake/Tacho"
)
